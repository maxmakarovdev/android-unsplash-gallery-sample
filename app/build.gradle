plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt' //ksp currently doesn't support Hilt
    id 'com.google.dagger.hilt.android'
}

android {
    namespace 'com.maxmakarov.gallery'
    compileSdk 33

    defaultConfig {
        applicationId "com.maxmakarov.gallery"
        minSdk 26
        //noinspection OldTargetApi
        targetSdk 33
        versionCode 1
        versionName "0.8"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        def secretKeys = loadSecretKeys()

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField 'String', 'UNSPLASH_ACCESS_KEY', secretKeys['access_key']
            buildConfigField 'String', 'UNSPLASH_SECRET_KEY', secretKeys['secret_key']
        }
        debug {
            minifyEnabled false
            buildConfigField 'String', 'UNSPLASH_ACCESS_KEY', secretKeys['access_key']
            buildConfigField 'String', 'UNSPLASH_SECRET_KEY', secretKeys['secret_key']
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    buildFeatures {
        viewBinding true
        compose true
        buildConfig true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.4.8'
    }
}

dependencies {
    implementation(project(":feature_gallery"))
    implementation(project(":feature_favorites"))
    implementation(project(":feature_settings"))
    implementation(project(":feature_view_image"))

    kapt "com.google.dagger:hilt-compiler:2.44"
    api "com.google.dagger:hilt-android:2.44"
}

def loadSecretKeys() {
    def keyFile = file("../unsplash.keys")
    def secretKeys = new Properties()
    secretKeys.load(new FileInputStream(keyFile))
    return secretKeys
}